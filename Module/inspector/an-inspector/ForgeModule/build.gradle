apply plugin: 'com.android.library'

android {
	compileSdkVersion rootProject.ext.compileSdkVersion
	buildToolsVersion rootProject.ext.buildToolsVersion

	defaultConfig {
		minSdkVersion rootProject.ext.minSdkVersion
		targetSdkVersion rootProject.ext.targetSdkVersion
		compileOptions rootProject.ext.compileOptions
	}

	publishNonDefault true

	flavorDimensions "crosswalk"

	productFlavors {
		vanilla {
		}
		crosswalk {
			minSdkVersion 14
			ndk {
			}
			dimension "crosswalk"
		}
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
			jniLibs.srcDirs = ['libs']
		}
		crosswalk {
			java.srcDirs = ["flavors/crosswalk/src"]
		}
		vanilla {
			java.srcDirs = ["flavors/vanilla/src"]
		}

		instrumentTest.setRoot('tests')
	}

	buildTypes {
		debug rootProject.ext.debug
		release rootProject.ext.release << {
			proguardFile getDefaultProguardFile("proguard-android.txt")
			proguardFile "../ForgeInspector/proguard-project.txt"
		}
	}

	lintOptions {
		abortOnError false
	}
}

ext {
    pushwoosh = "5.5.5"
}

dependencies {
	implementation fileTree(dir: "libs", include: ["*.jar"])

	implementation "com.android.support:support-v4:" + rootProject.ext.supportLibraryVersion
	implementation "com.android.support:appcompat-v7:" + rootProject.ext.supportLibraryVersion
	implementation "com.google.guava:guava:13.0.1"
    implementation "com.pushwoosh:pushwoosh:${pushwoosh}"
    implementation "com.pushwoosh:pushwoosh-amazon:${pushwoosh}"
    implementation "com.pushwoosh:pushwoosh-badge:${pushwoosh}"
    implementation "com.pushwoosh:pushwoosh-gcm:${pushwoosh}"
    implementation "com.google.android.gms:play-services-gcm:11.6.2"
	implementation "com.google.android.gms:play-services-location:11.6.2"


	crosswalkCompile "org.xwalk:xwalk_core_library:" + rootProject.ext.crosswalkVersion

	vanillaCompile (name:'ForgeCore-vanilla-debug', ext:'aar')
	crosswalkCompile (name:'ForgeCore-crosswalk-debug', ext:'aar')

	compile moduleDependencies()
}

repositories{
	flatDir {
		dirs 'libs'
	}
}

task updateModule (dependsOn: [":ForgeModule:assembleRelease", ":ForgeModule:assembleDebug"]) {
	doLast {
		// copy vanilla jar for backwards compatibility
		copy {
			from "build/intermediates/bundles/vanillaRelease/classes.jar"
			into "../../../module/android"
			rename { "module.jar" }
		}
		println "\n\tUpdated module/android/module.jar"

		// copy aar flavors for platforms >= 2.4.1
		copy {
			from "build/outputs/aar/ForgeModule-vanilla-release.aar"
			into "../../../module/android"
		}
		println "\tUpdated module/android/ForgeModule-vanilla-release.aar"

		copy {
			from "build/outputs/aar/ForgeModule-crosswalk-release.aar"
			into "../../../module/android"
		}
		println "\tUpdated module/android/ForgeModule-crosswalk-release.aar\n"
	}
}
